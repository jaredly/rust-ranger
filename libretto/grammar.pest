
file = _{SOI ~ toplevel_statement* ~ EOI}
expr = _{SOI ~ statement* ~ value ~ EOI}

toplevel_statement = {
    const_binding |
    value ~ ";" |
    fndefn
}
statement = {
    let_binding |
    value ~ ";" |
    fndefn
}
block = { "{" ~ statement* ~ value ~ "}" }

let_binding = {"let" ~ ident ~ "=" ~ value ~ ";"}
const_binding = {"const" ~ ident ~ ":" ~ "any" ~ "=" ~ value ~ ";"}

value = {cast ~ binop_post*}
binop_post = {binop ~ cast}

cast = { subject ~ ("as" ~ type_)?}

type_ = {"f32" | "i32"}

subject = { op_item ~ ("." ~ (fncall | ident | digits))* }
digits = @{ASCII_DIGIT+}

if_chain = {"if" ~ if_cond ~ block ~ ("else" ~ "if" ~ if_cond ~ block)* ~ ("else" ~ block)? }
if_cond = {"let" ~ pattern ~ "=" ~ value | value}

pattern = {"_" | const_ | struct_pattern | tuple_pattern | ident}
tuple_pattern = {upper_ident ~ ("(" ~ (pattern ~ ("," ~ pattern)* )? ~ ")")? }
struct_pattern = {upper_ident ~ "{" ~ ident ~ (":" ~ pattern)? ~ ("," ~ ident ~ (":" ~ pattern)?)* ~ "}" }

match_ = {"match" ~ value ~ "{" ~ pattern ~ "=>" ~ value ~ ("," ~ pattern ~ "=>" ~ value)* ~ "}"}

op_item = _{
    unit
    | "(" ~ value ~ ")"
    | block

    | if_chain
    | match_
    | fncall

    // TODO figure out scope lifetime rules
    // | lambda

    // Typed containers
    | struct_
    | option
    | named_tuple

    // Untyped containers
    | object
    | array

    // atoms
    | const_
    | ident
    | upper_ident
}
binop = { "+" | "-" | "*" | "/" | "==" | "!=" | "<" | ">" }
option = {"None" | "Some" ~ "(" ~ value ~ ")"}

fncall = { ident ~ "(" ~ comma_values? ~ ")" }
lambda = { "|" ~ args ~ "|" ~ op_item }
fndefn = { "fn" ~ ident ~ "(" ~ args ~ ")" ~ block }

args = { (ident ~ ":" ~ "any" ~ ("," ~ ident ~ ":" ~ "any")* ~ ","?)? }


unit = {"()"}

// TODO maybe punning?
struct_ = {
    upper_ident ~ "{" ~ comma_pairs ~ "}"
}

named_tuple = {
    upper_ident ~ "(" ~ comma_values ~ ")"
}

object = {
    "{" ~ comma_pairs? ~ "}"
}
comma_pairs = _{pair ~ ("," ~ pair)* ~ ","?}
pair = { (string | ident) ~ ":" ~ value }

array = {
    "[" ~ "]" |
    "[" ~ comma_values ~ "]"
}
comma_values = _{value ~ ("," ~ value)* ~ ","?}

json = _{object | array}









// implicit
WHITESPACE = _{ " " | "\n" | "\t" | "\r" }
COMMENT = _{ line_comment | block_comment }
line_comment = { "//" ~ line_comment_inner ~ (NEWLINE | &EOI) }
line_comment_inner = { (!NEWLINE ~ ANY)* }
block_comment = { "/*" ~ (block_comment | block_comment_inner)  ~ "*/" }
block_comment_inner = { (!"*/" ~ ANY)* }





ident = @{ (ASCII_ALPHA_LOWER | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
upper_ident = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHANUMERIC | "_")* }

const_ = {float | signed_int | bool | char | string}

// booleans
bool = { "true" | "false" }

// numbers
sign = { "+" | "-" }

signed_int = @{ sign? ~ unsigned_int }
unsigned_int = { with_base | ASCII_DIGIT+ }
with_base = { "0" ~ ("x" | "b" | "o") ~ ASCII_HEX_DIGIT+ }

float = @{ float_std | float_frac }
float_std = { sign? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ float_exp? }
float_frac = { "." ~ ASCII_DIGIT+ ~ float_exp? }
float_exp = { ("e" | "E") ~ ASCII_DIGIT+ }

// chars
char = ${ "'" ~ char_inner ~ "'" }
char_inner = { "\\'" | (!"'" ~ ANY)* }

// strings
string = ${ string_std | string_raw }

string_std = { "\"" ~ string_std_inner ~ "\"" }
string_std_inner = { ("\\\"" | !"\"" ~ ANY)* }

string_raw = { "r" ~ PUSH("#"*) ~ "\"" ~ string_raw_inner ~ "\"" ~ POP }
string_raw_inner = { (!("\"" ~ PEEK) ~ ANY)* }

